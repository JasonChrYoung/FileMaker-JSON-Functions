/**
 * =====================================
 * ~jsonParseError ( message )
 * 
 * RETURNS:
 *		Message parameter formatted as an error
 *
 * PARAMETERS:
 *		message = description of the error
 *
 * DEPENDENCIES:
 *		Custom Functions:
 *			~jsonEncodeString
 *
 * HISTORY:
 *		CREATED on 2015-MAR-31 by Daniel Smith dansmith65@gmail.com
 *
 * =====================================
 */

Let ( [
	/* save the error */
	$~json.error = List (
		message
			& If ( $~json.at > 0 ;
				" [position: " & $~json.at & "]"
				& " [character: " & $~json.ch & "]"
			)
		;
		$~json.error
	) ;
	/* trigger parent functions to abort */
	$~json.ch = "" ;
	$~json.at = -1
] ;
	~jsonEncodeString ( message )
)