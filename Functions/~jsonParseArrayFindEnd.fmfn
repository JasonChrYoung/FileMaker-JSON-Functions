/**
 * =====================================
 * ~jsonParseArrayFindEnd ( step )
 *
 * PURPOSE:
 *		Move pointer to the end of the array.
 *
 * RETURNS:
 *		Empty string.
 *		(can check $~json.error to see if an error occurred)
 *
 * PARAMETERS:
 *		step = always leave empty when calling this function
 *			(stores the current state of processing this recursive function is in)
 *
 * DEPENDENCIES:
 *		Pointer is expected to be at [ starting the array.
 *		Custom Functions:
 *			~jsonParseArrayFindEnd		(self: it's recursive!)
 *			~jsonParseError
 *			~jsonParseNext
 *			~jsonParseWhitespace
 *			~jsonParseValueSkip
 *
 * NOTES:
 *		Recursive and multiple instances of this function my be running at any
 *		given point in time, so $localVars cannot be used.
 *
 * HISTORY:
 *		CREATED on 2015-APR-02 by Daniel Smith dansmith65@gmail.com
 *
 * =====================================
 */

Case (
	/**
	 ***************************************************************************
	 * STEP 0: SET-UP
	 ***************************************************************************
	 */
	not step ;
	Let ( [
		~! = /* logging start */ LogWriterMemoryCreateEntry (
			"~jsonParseArrayFindEnd [step:0]"
		) & /* logging end */ "" ;
		
		~! = If ( IsEmpty ( ~jsonParseNext ( "[" ) ) ;
			~jsonParseError ( "Pointer was not at start of an array" )
		) ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			~jsonParseWhitespace
		) ;
		
		/* check if array is empty */
		~endFound = If ( IsEmpty ( $~json.error ) ;
			If ( $~json.ch = "]" ;
				True
					& ~jsonDiscardResult ( ~jsonParseNext ( "]" ) )
			)
		) ;
		
		~nextStep = If (
			not IsEmpty ( $~json.error )
				or ~endFound
			;
			2 ;	/* clean-up */
			1	/* main */
		)
	] ;
		~jsonParseArrayFindEnd ( ~nextStep )
	) ;
	
	
	/**
	 ***************************************************************************
	 * STEP 1: MAIN
	 ***************************************************************************
	 */
	step = 1 ;
	Let ( [
		~! = /* logging start */ LogWriterMemoryCreateEntry (
			"~jsonParseArrayFindEnd [step:1]"
		) & /* logging end */ "" ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			~jsonParseValueSkip
		) ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			~jsonParseWhitespace
		) ;
		
		/* check if at end of array */
		~endFound = If ( IsEmpty ( $~json.error ) ;
			If ( $~json.ch = "]" ;
				True
					& ~jsonDiscardResult ( ~jsonParseNext ( "]" ) )
			)
		) ;
		
		/* advance pointer to start of next value*/
		~! = If ( IsEmpty ( $~json.error ) and not ~endFound ;
			~jsonParseNext ( "," )
			& If ( IsEmpty ( $~json.error ) ;
				~jsonParseWhitespace
			)
		) ;
		
		~nextStep = If (
			not IsEmpty ( $~json.error )
				or ~endFound
			;
			2 ;	/* clean-up */
			1	/* main */
		)
	] ;
		~jsonParseArrayFindEnd ( ~nextStep )
	) ;
	
	
	/**
	 ***************************************************************************
	 * STEP 2: CLEAN-UP
	 ***************************************************************************
	 */
	step = 2 ;
	Let ( [
		~! = /* logging start */ LogWriterMemoryCreateEntry (
			"~jsonParseArrayFindEnd [step:2]"
		) & /* logging end */ "" ;
		
		/* no local variables to clean up */
		~! = ""
	] ;
		""
	)
)