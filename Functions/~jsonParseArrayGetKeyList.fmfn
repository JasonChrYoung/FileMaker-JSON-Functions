/**
 * =====================================
 * ~jsonParseArrayGetKeyList ( step )
 *
 * RETURNS:
 *		A list of top-level indexes in an array.
 *
 * PARAMETERS:
 *		step = always leave empty when calling this function
 *			(stores the current state of processing this recursive function is in)
 *
 * DEPENDENCIES:
 *		Pointer is expected to be at [ starting the array.
 *		Custom Functions:
 *			~jsonParseArrayGetKeyList		(self: it's recursive!)
 *			~jsonParseError
 *			~jsonParseNext
 *			~jsonParseWhitespace
 *
 * NOTES:
 *
 * HISTORY:
 *		CREATED on 2015-APR-04 by Daniel Smith dansmith65@gmail.com
 *
 * =====================================
 */

Case (
	/**
	 ***************************************************************************
	 * STEP 0: SET-UP
	 ***************************************************************************
	 */
	not step ;
	Let ( [
		~! = /* logging start disabled LogWriterMemoryCreateEntry (
			"~jsonParseArrayGetKeyList [step:0]"
		) & disabled logging end */ "" ;
		
		/* initialize variables */
		$~jsonParseArrayGetKeyList.i = -1 ;
		$~jsonParseArrayGetKeyList.result = "" ;
		
		~! = If ( IsEmpty ( ~jsonParseNext ( "[" ) ) ;
			~jsonParseError ( "Pointer was not at start of an array" )
		) ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			~jsonParseWhitespace
		) ;
		
		/* check if array is empty */
		~endFound = If ( IsEmpty ( $~json.error ) ;
			If ( $~json.ch = "]" ;
				True
					& ~jsonDiscardResult ( ~jsonParseNext ( "]" ) )
			)
		) ;
		
		~nextStep = If (
			not IsEmpty ( $~json.error )
				or ~endFound
			;
			2 ;	/* clean-up */
			1	/* main */
		)
	] ;
		~jsonParseArrayGetKeyList ( ~nextStep )
	) ;
	
	
	/**
	 ***************************************************************************
	 * STEP 1: MAIN
	 ***************************************************************************
	 */
	step = 1 ;
	Let ( [
		$~jsonParseArrayGetKeyList.i = $~jsonParseArrayGetKeyList.i + 1 ;
		
		~! = /* logging start disabled LogWriterMemoryCreateEntry (
			"~jsonParseArrayGetKeyList [step:1]"
				& " [i:" & $~jsonParseArrayGetKeyList.i & "]"
		) & disabled logging end */ "" ;
		
		~key = $~jsonParseArrayGetKeyList.i ;
		
		$~jsonParseArrayGetKeyList.result = If ( IsEmpty ( $~json.error ) ;
			List (
				$~jsonParseArrayGetKeyList.result ;
				~key
			)
		) ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			z_jsonParseSupport1 ( 101 /* ParseValueSkip */ ; "" ; "" ; "" ; "" )
		) ;
		
		~! = If ( IsEmpty ( $~json.error ) ;
			~jsonParseWhitespace
		) ;
		
		/* check if at end of array */
		~endFound = If ( IsEmpty ( $~json.error ) ;
			If ( $~json.ch = "]" ;
				True
					& ~jsonDiscardResult ( ~jsonParseNext ( "]" ) )
			)
		) ;
		
		/* advance pointer to start of next key */
		~! = If ( IsEmpty ( $~json.error ) and not ~endFound ;
			~jsonParseNext ( "," )
			& If ( IsEmpty ( $~json.error ) ;
				~jsonParseWhitespace
			)
		) ;
		
		~nextStep = If (
			not IsEmpty ( $~json.error )
				or ~endFound
			;
			2 ;	/* clean-up */
			1	/* main */
		)
	] ;
		~jsonParseArrayGetKeyList ( ~nextStep )
	) ;
	
	
	/**
	 ***************************************************************************
	 * STEP 2: CLEAN-UP
	 ***************************************************************************
	 */
	step = 2 ;
	Let ( [
		~result = If ( IsEmpty ( $~json.error ) ;
			$~jsonParseArrayGetKeyList.result ;
			"Error: " & $~json.error
		) ;
		
		~! = /* logging start disabled LogWriterMemoryCreateEntry (
			"~jsonParseArrayGetKeyList [step:2]"
				& " [result:" & ~result & "]"
		) & disabled logging end */ "" ;
		
		/* clean up local variables */
		$~jsonParseArrayGetKeyList.result = ""
	] ;
		~result
	)
)